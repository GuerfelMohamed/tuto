<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml"><head><title>Traditional web interaction in a client-server app</title><meta charset="utf8"/><link rel="stylesheet" href="/home/balat/ocsigen/ocsigen.github.io/css/style.css"/><link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/prism/1.9.0/themes/prism.min.css"/><script src="/home/balat/ocsigen/html_of_wiki/client.js"></script><script src="https://cdnjs.cloudflare.com/ajax/libs/prism/1.9.0/components/prism-core.min.js"></script><script src="https://cdnjs.cloudflare.com/ajax/libs/prism/1.9.0/components/prism-ocaml.min.js"></script><script src="https://cdnjs.cloudflare.com/ajax/libs/prism/1.9.0/components/prism-clike.min.js"></script><script src="https://cdnjs.cloudflare.com/ajax/libs/prism/1.9.0/components/prism-reason.min.js"></script><script src="https://cdnjs.cloudflare.com/ajax/libs/prism/1.9.0/components/prism-javascript.min.js"></script></head><body><h1>Traditional web interaction in a client-server app</h1><p><em>The code of this tutorial has been tested with Eliom 6.0.</em> <br/>
</p><h2>Multi-user collaborative drawing application</h2><p>We now want to turn our collaborative drawing application into a
multi-user one. Each user will have their own drawing, where everyone
can draw, and the owner will have the ability to save the drawing they
want and create an Atom feed containing all saved drawings.
</p><p>See the
<a href="https://github.com/ocsigen/graffiti/tree/master/extended/" class="ocsimore_phrasing_link">full code of examples</a>.
</p><h3>Split application into multiple files and using several canvases</h3><aside class="concepts"><header><h5>Concepts</h5></header><p>Complex eliom project<br/>Unique elements</p></aside><p>We first build a multi-canvas drawing application. Each drawing has
its own URL. Everyone can create a new drawing by going to the
corresponding URL.
</p><p>We need to refactor some parts. In particular, we need to handle
different drawings separately. To do this, we turn all global
variables, like the bus, into local ones.
</p><p>When an application grows, it becomes useful to split it into multiple
files. For example, we will split graffiti into 4 files.
</p><ul><li> <span class="teletype">common.ml</span>, which will be part of both client and server,
containing shared types and declarations,
</li><li> <span class="teletype">client.ml</span>, client-only part of the application,
</li><li> <span class="teletype">server.ml</span>, server-only part of the application, and
</li><li> <span class="teletype">graffiti.eliom</span>, which is the only part where we need to
include both client-side and server-side code
</li></ul><h4><span class="teletype">common.ml</span></h4><p>It contains what was previously in <span class="teletype"> [%shared ... ] </span>
</p><pre class=""><code class="language-ocaml translatable">type messages =
  ((int * int * int) * int * (int * int) * (int * int))
  [@@deriving json]

let width = 700
let height = 400</code></pre><h4><span class="teletype">client.ml</span></h4><p>It is almost the same code as what was enclosed in {{{ {client{ ... }}
}}}, with the difference that what was previously in the client value
<span class="teletype">init_client</span> is now in the function <span class="teletype">launch_client_canvas</span>.
</p><pre class=""><code class="language-ocaml translatable">open Common
open Eliom_content

let draw ctx ((r, g, b), size, (x1, y1), (x2, y2)) =
  let color = CSS.Color.string_of_t (CSS.Color.rgb r g b) in
  ctx##.strokeStyle := (Js.string color);
  ctx##.lineWidth := float size;
  ctx##beginPath;
  ctx##(moveTo (float x1) (float y1));
  ctx##(lineTo (float x2) (float y2));
  ctx##stroke

(* type containing all informations we need to stop interaction
   inside the page *)
type drawing_canceller =
    { message_thread : unit Lwt.t;
      (* the thread reading messages from the bus *)
      drawing_thread : unit Lwt.t;
      (* the arrow handling mouse events *)
    }

let stop_drawing { message_thread; drawing_thread } =
  Lwt.cancel message_thread;
  (* cancelling this thread also close the bus *)
  Lwt.cancel drawing_thread</code></pre><p><span class="teletype">Lwt.cancel t</span> stops thread t. In this case it also closes the
bus on which t is listening. For more informations see the
<span><a class="ocsforge_doclink_lwt" href="/lwt/3.1.0/manual/manual">Lwt programming guide </a></span> and
<span><a class="ocsforge_doclink_eliom" href="/eliom/6.2/api/client/Eliom_bus">Eliom_bus</a></span>.
</p><pre class=""><code class="language-ocaml translatable">let launch_client_canvas bus image_elt canvas_elt slider =
  let canvas = Html.To_dom.of_canvas canvas_elt in
  let ctx = canvas##(getContext (Dom_html._2d_)) in
  ctx##.lineCap := Js.string &quot;round&quot;;

  let img = Html.To_dom.of_img image_elt in
  let copy_image () = ctx##(drawImage img (0.) (0.)) in
  if Js.to_bool (img##.complete)
  then copy_image ()
  else img##.onload := Dom_html.handler
    (fun ev -&gt; copy_image (); Js._false);

  (* The color palette: *)
  let colorpicker = Ow_color_picker.create ~width:150 () in
  Ow_color_picker.append_at (Dom_html.document##.body) colorpicker;
  Ow_color_picker.init_handler colorpicker;

  let x = ref 0 and y = ref 0 in
  let set_coord ev =
    let x0, y0 = Dom_html.elementClientPosition canvas in
    x := ev##.clientX - x0; y := ev##.clientY - y0 in
  let compute_line ev =
    let oldx = !x and oldy = !y in
    set_coord ev;
    let rgb = Ow_color_picker.get_rgb colorpicker in
    let size_slider = Html.To_dom.of_input slider in
    let size = int_of_string (Js.to_string size_slider##.value) in
    (rgb, size, (oldx, oldy), (!x, !y))
  in
  let line ev =
    let v = compute_line ev in
    let _ = Eliom_bus.write bus v in
    draw ctx v;
    Lwt.return ()
  in
  let t = Lwt_stream.iter (draw ctx) (Eliom_bus.stream bus) in
  let drawing_thread =
    Lwt_js_events.(
      mousedowns canvas (fun ev elt -&gt;
        Dom.preventDefault ev;
        set_coord ev;
        let%lwt () = line ev in
        Lwt.pick [mousemoves Dom_html.document (fun a _ -&gt; line a);
	          let%lwt ev = mouseup Dom_html.document in line ev]))
  in
  { message_thread = t;
    drawing_thread = drawing_thread }</code></pre><h4><span class="teletype">server.ml</span></h4><p>It contains almost all the server parts of the code.
</p><pre class=""><code class="language-ocaml translatable">open Eliom_content
open Common
open Lwt

module My_app =
  Eliom_registration.App (struct
    let application_name = &quot;graffiti&quot;
    let global_data_path = None
  end)</code></pre><p>The main difference is that the bus is now local.
</p><pre class=""><code class="language-ocaml translatable">let launch_server_canvas () =
  let bus = Eliom_bus.create [%derive.json: messages] in

  let draw_server, image_string =
    let rgb_ints_to_floats (r, g, b) =
      float r /. 255., float g /. 255., float b /. 255. in
    let surface = Cairo.Image.create Cairo.Image.ARGB32 ~width ~height in
    let ctx = Cairo.create surface in
    ((fun (rgb, size, (x1, y1), (x2, y2)) -&gt;

      (* Set thickness of brush *)
      let r, g, b = rgb_ints_to_floats rgb in
      Cairo.set_line_width ctx (float size) ;
      Cairo.set_line_join ctx Cairo.JOIN_ROUND ;
      Cairo.set_line_cap ctx Cairo.ROUND ;
      Cairo.set_source_rgb ctx ~r ~g ~b ;

      Cairo.move_to ctx (float x1) (float y1) ;
      Cairo.line_to ctx (float x2) (float y2) ;
      Cairo.Path.close ctx ;

      (* Apply the ink *)
      Cairo.stroke ctx ;
     ),
     (fun () -&gt;
       let b = Buffer.create 10000 in
       (* Output a PNG in a string *)
       Cairo.PNG.write_to_stream surface (Buffer.add_string b);
       Buffer.contents b
     ))
  in
  let _ = Lwt_stream.iter draw_server (Eliom_bus.stream bus) in
  bus,image_string

let graffiti_info = Hashtbl.create 0

let imageservice =
  Eliom_registration.String.create
    ~path:(Eliom_service.Path [&quot;image&quot;])
    ~headers:Http_headers.dyn_headers
    ~meth:
      (Eliom_service.Get
         (let open Eliom_parameter in string &quot;name&quot; ** int &quot;q&quot;))
    (* we add another parameter for the browser not to cache: at least
       for chrome, there is no way to force the browser to reload the
       image without leaving the application *)
    (fun (name,_) () -&gt;
      try%lwt
        let _ ,image_string = Hashtbl.find graffiti_info name in
	Lwt.return (image_string (), &quot;image/png&quot;)
      with
	| Not_found -&gt; [%lwt raise Eliom_common.Eliom_404])

let get_bus (name:string) =
  (* create a new bus and image_string function only if it did not exists *)
  try
    fst (Hashtbl.find graffiti_info name)
  with
    | Not_found -&gt;
      let bus,image_string = launch_server_canvas () in
      Hashtbl.add graffiti_info name (bus, image_string);
      bus</code></pre><p>The main page now contains only a form to choose to which drawing you
want to go.  The drawing will be sent by the
<span class="teletype">multigraffiti_service</span> service, registered in
<span class="teletype">graffiti.eliom</span>.
</p><pre class=""><code class="language-ocaml translatable">let main_service =
  Eliom_service.create
    ~path:(Eliom_service.Path [&quot;&quot;])
    ~meth:(Eliom_service.Get (Eliom_parameter.unit))
    ()

let multigraffiti_service =
  Eliom_service.create
    ~path:(Eliom_service.Path [&quot;&quot;])
    ~meth:(Eliom_service.Get (Eliom_parameter.(suffix (string &quot;name&quot;))))
    ()

let choose_drawing_form () =
  Html.D.Form.get_form ~service:multigraffiti_service
    (fun (name) -&gt;
       [Html.D.p [
           Html.D.pcdata &quot;drawing name: &quot;;
           Html.D.Form.input ~input_type:`Text ~name
             Html.D.Form.string;
           Html.D.br ();
           Html.D.Form.input ~input_type:`Submit ~value:&quot;Go&quot;
             Html.D.Form.string
         ]])

let oclosure_script =
  Html.Id.create_global_elt
    (Html.D.js_script
       ~uri:(Html.D.Raw.uri_of_string &quot;./graffiti_oclosure.js&quot;) ())

let make_page body =
  Lwt.return
    (Html.D.html
       (Html.D.head
	  (Html.D.title (Html.D.pcdata &quot;Graffiti&quot;))
 	  [
	    Html.D.css_link
	      ~uri:(Html.D.Raw.uri_of_string&quot;./css/closure/common.css&quot;) ();
	    Html.D.css_link
	      ~uri:(Html.D.Raw.uri_of_string&quot;./css/closure/hsvpalette.css&quot;) ();
	    Html.D.css_link
	      ~uri:(Html.D.Raw.uri_of_string&quot;./css/slider.css&quot;) ();
            oclosure_script;
	    Html.D.css_link
	      ~uri:(Html.D.Raw.uri_of_string&quot;./css/graffiti.css&quot;) ();
          ])
       (Html.D.body body))

let () = My_app.register ~service:main_service
  (fun () () -&gt;
    make_page [h1 [pcdata &quot;Welcome to Multigraffiti&quot;];
	       choose_drawing_form ()])</code></pre><aside class="concept"><header><h5><span class="concept_prefix">Concept: </span>Global node and scripts</h5></header><p>Sometimes we need to control when a script is loaded and reloaded on
page change. Since clicking on links in an Eliom application do not
reload the entire page, already loaded libraries stay loaded. But if
we simply add a script to each page, it will be loaded each time.
We usually don't want that.
</p><p>Using <span><a class="ocsforge_doclink_eliom" href="/eliom/6.2/api/server/Eliom_content.Html.Id#VALcreate_global_elt">Eliom_content.​Html.​Id.​create_global_elt</a></span> we can create an xml node
manipulated 'by reference'. If a reference to a script node is
included in a page, it will only be loaded the first time it appears
in the header. This is the case for <span class="teletype">oclosure_script</span> above.</p></aside><h4><span class="teletype">graffiti.eliom</span></h4><p>Here is the code that mixes client and server parts.
</p><p>We first open the corresponding modules for each parts of the
application.
</p><pre class=""><code class="language-ocaml translatable">[%%shared
    open Eliom_content.Html.D
    open Common
]
[%%client
    open Client
]
open Server</code></pre><p>And then we define a function initializing the client application by
side effects in a client value.
</p><pre class=""><code class="language-ocaml translatable">let start_drawing name image canvas slider =
  let bus = get_bus name in
  ignore [%client
    (let canceller =
       launch_client_canvas ~%bus ~%image ~%canvas ~%slider
     in
     Eliom_client.onunload (fun () -&gt; stop_drawing canceller; None)
     : unit)
  ]</code></pre><p>The function registered by <span><a class="ocsforge_doclink_eliom" href="/eliom/6.2/api/client/Eliom_service#VALonunload">Eliom_service.​onunload</a></span> will be called when
the page change inside the application.
</p><p>And we finally register the service sending a drawing:
</p><pre class=""><code class="language-ocaml translatable">let counter = ref 0

let () =
  My_app.register ~service:multigraffiti_service (fun name () -&gt;
    (* Some browsers won't reload the image, so we force
          them by changing the url each time. *)
    incr counter;
    let image =
      img ~alt:name
        ~src:(make_uri ~service:imageservice (name,!counter)) ()
    in
    let slider =
      Form.input
        ~a:[
          a_id &quot;slider&quot;;
          a_input_min (`Number 1);
          a_input_max (`Number 80)
        ]
        ~input_type:`Range
        Form.int
    in
    let canvas =
      canvas ~a:[a_width width; a_height height]
        [pcdata &quot;your browser doesn't support canvas&quot;; br (); image]
    in
    start_drawing name image canvas slider;
    make_page
      [h1 [pcdata name];
       choose_drawing_form ();
       canvas;
       div [slider]])</code></pre><p>At this point, you can run your application on the server provided
that you installed the css and images directories in the main
directory of your application, build it using this
<a href="https://github.com/ocsigen/graffiti/tree/master/extended/Makefile" class="ocsimore_phrasing_link">Makefile</a>
along with the appropriate
<a href="https://github.com/ocsigen/graffiti/tree/master/extended/Makefile.options" class="ocsimore_phrasing_link">Makefile.options</a>,
and configured it using
<a href="https://github.com/ocsigen/graffiti/tree/master/extended/graffiti.conf.in" class="ocsimore_phrasing_link">graffiti.conf.in</a>,
as the basis for your configuration file.
</p><h3>Mixing client-server application with traditional web interaction</h3><p>We now want to restrict the site to connected users.
</p><p>From the previous chapter, we copy the code handling users to
<span class="teletype">server.ml</span>:
</p><pre class=""><code class="language-ocaml translatable">let connection_service = Eliom_service.create
    ~path:Eliom_service.No_path
    ~meth:(Eliom_service.Post (
      Eliom_parameter.unit,
      Eliom_parameter.(string &quot;name&quot; ** string &quot;password&quot;)
    ))
    ()

let disconnection_service = Eliom_service.create
    ~path:Eliom_service.No_path
    ~meth:(Eliom_service.Post (Eliom_parameter.unit, Eliom_parameter.unit))
    ()

let create_account_service = Eliom_service.create_post
    ~fallback:main_service
    ~post_params:(Eliom_parameter.(string &quot;name&quot; ** string &quot;password&quot;))
    ()

let user_table = Ocsipersist.open_table &quot;user_table&quot;

let check_pwd name pwd =
  try%lwt
    let%lwt saved_password = Ocsipersist.find user_table name in
    Lwt.return (pwd = saved_password)
  with Not_found -&gt; Lwt.return false

let () = Eliom_registration.Action.register
  ~service:create_account_service
  (fun () (name, pwd) -&gt; Ocsipersist.add user_table name pwd)

let () = Eliom_registration.Action.register
  ~service:connection_service
  (fun () (name, password) -&gt;
    match%lwt check_pwd name password with
      | true -&gt; Eliom_state.set_volatile_data_session_group
	~scope:Eliom_common.default_session_scope name;
	Lwt.return ()
      | false -&gt; Lwt.return ())

let () =
  Eliom_registration.Action.register
    ~service:disconnection_service
    (fun () () -&gt;
      Eliom_state.discard ~scope:Eliom_common.default_session_scope ())

let disconnect_box () =
  Html.D.Form.post_form disconnection_service
    (fun _ -&gt;
       [Html.D.p [
           Html.D.Form.input
             ~input_type:`Submit ~value:&quot;Log out&quot;
             Html.D.Form.string
         ]
       ]) ()

let login_name_form service button_text =
  Html.D.Form.post_form ~service
    (fun (name1, name2) -&gt;
       [Html.D.p [
           Html.D.pcdata &quot;login: &quot;;
           Html.D.Form.input ~input_type:`Text ~name:name1
             Html.D.Form.string;
           Html.D.br ();
           Html.D.pcdata &quot;password: &quot;;
           Html.D.Form.input ~input_type:`Password ~name:name2
             Html.D.Form.string;
           Html.D.br ();
           Html.D.Form.input ~input_type:`Submit ~value:button_text
             Html.D.Form.string
         ]]) ()</code></pre><p>We make a customized registration module such that disconnected users
(those for which the username reference is not set), are automaticaly
shown a connection box. This way the other pages can assume that the
username is always available.
</p><pre class=""><code class="language-ocaml translatable">let default_content () =
  make_page
    [Html.D.h1 [Html.D.pcdata &quot;Welcome to Multigraffiti&quot;];
     Html.D.h2 [Html.D.pcdata &quot;log in&quot;];
     login_name_form connection_service &quot;Connect&quot;;
     Html.D.h2 [Html.D.pcdata &quot;create account&quot;];
     login_name_form create_account_service &quot;Create account&quot;;]

module Connected_translate =
struct
  type page = string -&gt; My_app.page Lwt.t
  let translate page =
    match Eliom_state.get_volatile_data_session_group
      ~scope:Eliom_common.default_session_scope () with
	| None -&gt; default_content ()
	| Some username -&gt; page username
end

module Connected =
  Eliom_registration.Customize (My_app) (Connected_translate)</code></pre><p>We replace the previous <span class="teletype">main_service</span> registration :
</p><pre class=""><code class="language-ocaml translatable">let () = My_app.register ~service:main_service
  (fun () () -&gt;
    make_page [h1 [pcdata &quot;Welcome to Multigraffiti&quot;];
	       choose_drawing_form ()])</code></pre><p>by :
</p><pre class=""><code class="language-ocaml translatable">let ( !% ) f = fun a b -&gt; return (fun c -&gt; f a b c)

let () = Connected.register
  ~service:main_service
  !% (fun () () username -&gt;
    make_page
      [Html.D.h1 [Html.D.pcdata (&quot;Welcome to Multigraffiti &quot; ^ username)];
       choose_drawing_form ()])</code></pre><p>to use that, in <span class="teletype">graffiti.eliom</span> we just replace add a call
to <span class="teletype">disconnect_box</span>
</p><pre class=""><code class="language-ocaml translatable">[h1 [pcdata name];
       disconnect_box ();
       choose_drawing_form ();
       canvas;])</code></pre><p><a href="/tuto/6.1/manual/interaction" class="ocsimore_phrasing_link">prev</a>
</p></body></html>
